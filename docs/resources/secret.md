---
page_title: "gamefabric_secret Resource - GameFabric"
subcategory: ""
description: |-
  
---

# gamefabric_secret (Resource)

A Secret resource in GameFabric allows you to securely store sensitive data (credentials, API keys, passwords, tokens, etc.) that can be referenced and used across your infrastructure. Secrets are scoped to environments and provide a centralized way to manage sensitive information that can be shared across multiple deployments.

## What is a Secret?

Secrets are key-value data objects that store sensitive configuration information. They can be:

- **Mounted as files** in the filesystem of containers in your Armadas, ArmadaSets, Vessels, and Formations.
- **Referenced in environment variables** across all resources that support environment variable sources.
- **Organized with labels** to enable filtering and discovery of related secrets.

## Storage Options: `data` vs `data_wo`

GameFabric Secrets support two different storage modes depending on your security requirements:

### Regular Data (`data` attribute)
Use the `data` attribute when you need to:
- Store secret values persistently in Terraform state
- Keep secret values visible in your state file (your state should be secured)
- Manage secret values that don't change frequently

The `data` attribute values are stored in the Terraform state file and marked as sensitive, so they won't appear in logs or console output.

### Write-Only Data (`data_wo` attribute)
Use the `data_wo` attribute when you need to:
- Pass temporary secret values during resource creation or updates
- Avoid storing sensitive values in Terraform state
- Keep secrets ephemeral and only used during operations
- Maximize security by not persisting values

Values in `data_wo` are write-only: they're transmitted to the server but never stored in state or displayed in plans.
This is the recommended approach for highly sensitive credentials like API keys or temporary tokens.

In order to allow Terraform to detect changes to write-only secrets,
you must also set the `data_wo_version` attribute, when ever you change the secrets in `data_wo`.
If you do not change the `data_wo_version`, Terraform will not detect any changes and apply no change to the secret resource.

**Note:** Only one of `data` or `data_wo` can be specified for a secret resource. Choose based on your security requirements.

## Example Usage

```terraform
# Example 1: Secret with persistent data (will be stored in state)
resource "gamefabric_secret" "db_credentials" {
  name        = "examplesecret"
  environment = data.gamefabric_environment.dev.name
  description = "Backend database credentials"
  labels = {
    game       = "my_first_game"
    component  = "database"
  }
  data = {
    db_user     = "dbuser123"
    db_password = "abcdef123456!#$&'*+-=?^_`{|}~"
  }
}

# Example 2: Secret with write-only data (will NOT be stored in state)
resource "gamefabric_secret" "api_key" {
  name        = "examplesecret"
  environment = data.gamefabric_environment.prod.name
  description = "API key for external service"
  labels = {
    game       = "my_second_game"
    component  = "api"
  }

  # Write-only attributes require Terraform v1.11 and later.
  data_wo = {
    api_key    = "sk-1234567890abcdef"
    api_secret = "secret-xyz-9876543210"
  }
}
```

## Write-only secret data with an ephemeral resource

**Note**: Ephemeral resources are available in Terraform 1.10 and later, write-only attributes in 1.11 and later.

This example demonstrates how to use a write-only secret data with an ephemeral resource.

```terraform
# Example 3: Secret with write-only ephemeral data (will NOT be stored in state)
#
# Ephemeral resources are available in Terraform v1.10 and later.
ephemeral "random_password" "db_password" {
  length           = 16
  override_special = "!#$%&*()-_=+[]{}<>:?"
}

resource "gamefabric_secret" "db_credentials" {
  name        = "db-creds"
  environment = data.gamefabric_environment.prod.name
  description = "Database credentials"

  # Write-only attributes require Terraform v1.11 and later.
  data_wo = {
    db_user    = "secr3t_user"
    api_secret = ephemeral.random_password.db_password.result
  }
  data_wo_version = 1
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `environment` (String) The name of the environment the resource belongs to.
- `name` (String) The unique object name within its scope. Must contain only lowercase alphanumeric characters, hyphens, or dots. Must start and end with an alphanumeric character. Maximum length is 63 characters.

### Optional

> **NOTE**: [Write-only arguments](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments) are supported in Terraform 1.11 and later.

- `annotations` (Map of String) Annotations is an unstructured map of keys and values stored on an object.
- `data` (Map of String, Sensitive) Data contains the secret key-value pairs. These are stored in state and persisted. Use data_wo for write-only ephemeral values.
- `data_wo` (Map of String, Sensitive, [Write-only](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments)) DataWO is a write-only version of data. Values are sensitive and write-only - they are only transmitted to the server and never displayed or stored in state.
- `data_wo_version` (Number) DataWOVersion is the version of the write-only data. This is used to force updates when using data_wo.
- `description` (String) Description is the optional description of the secret.
- `labels` (Map of String) A map of keys and values that can be used to organize and categorize objects.

### Read-Only

- `id` (String) The unique Terraform identifier.

## Notes on Import

This resource **does not support import** because the GameFabric API returns masked secret data (not the actual values) when reading secrets. Since Terraform cannot determine the actual secret values from the API response, importing would result in incomplete state.

To manage secrets, create them as new resources in your Terraform configuration with the appropriate `data` or `data_wo` values.

