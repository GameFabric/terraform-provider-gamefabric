---
page_title: "gamefabric_imageupdater Resource - GameFabric"
subcategory: ""
description: |-
  
---

# gamefabric_imageupdater (Resource)

The image updater is a resource that allows you to automatically update the container image used by a target resource (like an Armada, Formation, Vessel, or ArmadaSet) whenever a new image tag is pushed to a specified image in the GameFabric container registry.
This is the mechanism that is used if you select "autoUpdate" for the container image in the GameFabric web interface when creating or updating an Armada, Formation, Vessel, or ArmadaSet.

Because this mechanism happens outside of terraform it is required to make use of the `lifecycle.ignore_changes` block on the target resource to prevent terraform from trying to revert the image back to the original value.


## Example Usage

This example sets up a minimal armada in a single region for europe and creates an image updater to automatically update the container image.

```terraform
resource "gamefabric_imageupdater" "name" {
  branch = data.gamefabric_branch.prod.name
  image  = data.gamefabric_image.gameserver.image
  target = gamefabric_armada.this.imageupdater_target
}


data "gamefabric_branch" "prod" {
  display_name = "prod"
}

data "gamefabric_image" "gameserver" {
  branch = data.gamefabric_branch.prod.name
  image  = "gameserver"
  tag    = "latest"
}

resource "gamefabric_armada" "this" {
  lifecycle {
    ignore_changes = [
      containers[0].image, # Prevents terraform diffs when image is updated by the image updater
    ]
  }
  name        = "myarmada"
  environment = data.gamefabric_environment.prod.name

  region = data.gamefabric_region.europe.name
  replicas = [
    {
      region_type  = "baremetal"
      min_replicas = 10
      max_replicas = 200
      buffer_size  = 10
    }
  ]
  containers = [
    {
      name  = "default" # the gameserver container should always be named "default"
      image = data.gamefabric_image.gameserver.image_target
      resources = {
        requests = {
          cpu    = "250m"
          memory = "256Mi"
        }
      }
      ports = [
        {
          name           = "game"
          protocol       = "TCP"
          container_port = 27015
          policy         = "Dynamic"
        }
      ]
    }
  ]
}
```


<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `branch` (String) The branch that contains the image.
- `image` (String) The name of the image to update.
- `target` (Attributes) Target configuration specifying the resource to update. (see [below for nested schema](#nestedatt--target))

### Read-Only

- `id` (String) The unique Terraform identifier.

<a id="nestedatt--target"></a>
### Nested Schema for `target`

Required:

- `environment` (String) The environment in which the target resource operates.
- `name` (String) The name of the target resource.
- `type` (String) The type of the target resource.